/*
Script generated by Aqua Data Studio 8.0.2 on Jan-15-2010 01:50:27 PM
Database: TrackerPal
Schema: <All Schemas>
Objects: PROCEDURE
*/
CREATE PROCEDURE [OffenderGetAllDetailsByAgencyIDOfficerID] (
    @AgencyID INT,
    @OfficerID INT, 
    @RoleID INT, 
    @UserID INT = -1
)
AS

IF @RoleID <> 6    
    BEGIN    
        SELECT DISTINCT Offender.OffenderID ,
            ISNULL(Offender.LastName, '') + ', '+ ISNULL(Offender.FirstName, '') AS 'OffenderName',
            Offender.AgencyID,
            Offender_Officer.OfficerID,
            ISNULL(Officer.LastName, '')  + ', ' + ISNULL(Officer.MiddleName, '') + ' ' +  ISNULL(Officer.FirstName, '') AS 'OfficerName',		
            Agency.Agency,
            ISNULL(TrackerAssignment.TrackerID,0) AS TrackerID, 
            ISNULL(Tracker.TrackerNumber,0) AS TrackerNumber
	    FROM Offender        
            LEFT JOIN Offender_Officer  ON Offender.OffenderID = Offender_Officer.OffenderID     
            INNER JOIN Agency ON Offender.AgencyID = Agency.AgencyID           
            LEFT JOIN Officer ON Officer.OfficerID = Offender_Officer.Officerid 
		    LEFT JOIN TrackerAssignment ON TrackerAssignment.TrackerAssignmentID = (SELECT MAX(TrackerAssignmentID) 
		    	                                                                    FROM [TrackerAssignment] ta 
			                                                                        WHERE ta.OffenderID = Offender.OffenderID 
			                                                                          AND ta.TrackerAssignmentTypeID = 1)	
		    LEFT JOIN Tracker ON Tracker.TrackerID = TrackerAssignment.TrackerID
	    WHERE (
                (       
                      (@RoleID = 2 OR @RoleID = 3)       
                  AND (Offender.AgencyID = @AgencyID)
                )      
                OR 
                (
                   (
                         (@AgencyID <= 0)        
                      or (Offender.AgencyID = @AgencyID)
                   )       
                   AND
                   (
                        (@OfficerID <= 0)        
                     or (Offender_Officer.OfficerID = @OfficerID)
                   )
                 )
               )            
         AND Offender.Deleted = 0 
         AND Offender.Victim = 0  
--         AND TrackerAssignment.TrackerID IS NOT NULL  
    	ORDER BY [OffenderName]  
    END  
ELSE 
    BEGIN   
        SELECT Offender.OffenderID ,
               DistributorEmployee.UserID,
    		   ISNULL(Offender.LastName, '') + ', ' + ISNULL(Offender.FirstName, '') AS 'OffenderName',   
    		   Offender.AgencyID,
               Offender_Officer.OfficerID,
               ISNULL(Officer.LastName, '') + ', ' +  ISNULL(Officer.MiddleName, '') + ' ' +  ISNULL(Officer.FirstName, '') AS 'OfficerName',		
               Agency.Agency 
        FROM Offender 
            INNER JOIN Agency on Agency.AgencyID = Offender.AgencyID    
            INNER JOIN DistributorEmployee ON DistributorEmployee.DistributorID = Agency.DistributorID
                                           AND DistributorEmployee.UserID = @UserID    
            LEFT JOIN Offender_Officer ON Offender_Officer.OffenderID = Offender.OffenderID     
            LEFT JOIN Officer ON Officer.OfficerID = Offender_Officer.OfficerID 
        WHERE Offender.Deleted = 0 
            AND Offender.Victim = 0    
        ORDER BY Offender.LastName, 
                 Offender.FirstName
    END
--EndIF

GO
GRANT EXECUTE ON [OffenderGetAllDetailsByAgencyIDOfficerID] TO [db_dml]
GO
